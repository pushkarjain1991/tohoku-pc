! $Id: generate_covar.F90 1604 2016-05-30 06:42:16Z lnerger $
!BOP
!
! !Program: generate_covar --- Compute covariance matrix from state trajectory
!
! !INTERFACE:
PROGRAM generate_covar

! !DESCRIPTION:
! This programm to computes a covariance matrix from a
! model trajectory of Lorenz96. The matrix is decomposed in
! EOFs and stored in form of eigenvalues and eigenvectors.
!
! The matrix is generated by a singular value decomposition
! of the perturbation matrix of a long state trajectory
! about its long time mean.

! !USES:
  IMPLICIT NONE

! Local variables
  INTEGER :: i, iter, rank      ! Counters
  INTEGER :: dim_state                   ! Dimensions
  REAL :: limit                                 ! Lower limit for singular values
  REAL, ALLOCATABLE :: states(:, :)             ! State trajectory
  REAL, ALLOCATABLE :: meanstate(:)             ! Mean state of trajectory
  INTEGER :: status_covar                             ! Status Flag
  REAL,ALLOCATABLE,DIMENSION(:) :: svals        ! field of singular values
  REAL,ALLOCATABLE,DIMENSION(:,:) :: svdU       ! left singular vectors

  ! Controls for PDAF_eofcovar
  INTEGER :: remove_mean  ! (1) Let PDAF_eofcovar compute and subtract the mean state;
                          ! (0) mean already removed from trajectory
  INTEGER :: do_mv        ! (1) to perform multivariate normalization; (0) no normalization

  ! Unused, but required variables for PDAF_eofcovar
  INTEGER :: nfields        ! Number of fields
  INTEGER :: dim_fields(1)  ! Size of field (only for multivariate scaling (not used here))
  INTEGER :: offsets(1)     ! Offset of fields (only for multivariate scaling (not used here))
  INTEGER :: stddev(1)      ! STDDEV of field (only for multivariate scaling (not used here))

  !My variables
  CHARACTER(len=4) :: ensstr
  CHARACTER(len=120) :: statefile
  integer :: dim_ens
  integer :: new_iter
!  integer :: desired_rank


! ************************************************
! *** Configuration                            ***
! ************************************************

  ! Lower limit for eigenvalue
  limit = 1.0e-12


! ************************************************
! *** Init                                     ***
! ************************************************

  WRITE (*,'(10x,a)') '*******************************************'
  WRITE (*,'(10x,a)') '*             GENERATE_COVAR              *'
  WRITE (*,'(10x,a)') '*                                         *'
  WRITE (*,'(10x,a)') '*    Compute covariance matrix and mean   *'
  WRITE (*,'(10x,a)') '*     state from a sequence of states.    *'
  WRITE (*,'(10x,a)') '*                                         *'
  WRITE (*,'(10x,a)') '*   Write covar matrix as scaled eigen-   *'
  WRITE (*,'(10x,a)') '*    vectors and singular values into     *'
  WRITE (*,'(10x,a)') '*              NetCDF file                *'
  WRITE (*,'(10x,a/)') '*******************************************'


! ************************************************
! *** Open trajectory file and read dimensions ***
! ************************************************

  ! Get dimensions
  !State dimension
  dim_state = 22000
  print *, "Dim_ens = "
  read(*,*) dim_ens
  print *, "dim_ens = ", dim_ens
!  desired_rank = 9

  WRITE (*,'(/1x,a)') 'Dimensions of experiment:'
  WRITE (*,'(5x,1x,a,i10)') 'state dimension', dim_state
  WRITE (*,'(11x,a,i10)')   'dim_ens', dim_ens

! ****************************
! *** Read trajectory data ***
! ****************************

  WRITE (*,'(/1x,a)') '------- Read trajectory -------------'

  ALLOCATE(states(dim_state, dim_ens))
  ALLOCATE(meanstate(dim_state))

  read_in: DO iter = 1, dim_ens
      new_iter = iter - 1
      write(ensstr, '(i3.1)') new_iter
      statefile = trim(adjustl('/h2/pkjain/Desktop/Pushkar/clawpack/geoclaw/examples&
      &/tsunami/tohoku-pc/Pre_ens_gen/_output_100/state.q'//&
      &trim(adjustl(ensstr))))
      open(22, file=statefile, status='old')
      read(22, *) states(1:dim_state, iter)
      close(22)

  END DO read_in

! *********************************************************
! *** Singular value decomposition of covariance matrix ***
! ***                                                   ***
! *** The covariance matrix is given by the state       ***
! *** sequences X of k states as                        ***
! ***          -1    _     _ T        T                 ***
! *** P = (k-1)   (X-X) (X-X)  = U L U  (EVP)           ***
! ***                                                   ***
! *** Thus we compute the singular value decomposition  ***
! ***     _        T            -1    2  T              ***
! ***   X-X = U S V ;  P = (k-1)   U S  U               ***
! ***                                                   ***
! ***                         -1/2                      ***
! *** and we store U and (k-1)     S in a NetCDF file.  ***
! *********************************************************

  WRITE (*,'(/1x,a)') '------- Compute covariance matrix decomposition -------------'

  ! PDAF_eofcovar should compute and subtract the mean state from Trajectory 
  ! before decomposition. Afterwards, it's added again.
  remove_mean = 1

  ! Set some variables only used for multivariate normalization - not here!
  nfields = 1      ! One field
  dim_fields = 22000  ! Size of field
  offsets = 0      ! offset of field from beginning of vector
  do_mv = 0   ! For testing one cat set do_mv=1 to activate normalization
  ! Note: multivariate normalization can be useful if there are several fields
  !       with difference variances. Here, we only have one field

  ! Allocate arrays for singular values and vectors
  ALLOCATE(svals(dim_ens))
  ALLOCATE(svdU(dim_state, dim_ens))

  ! Call routine generating matrix decomposition
  CALL PDAF_eofcovar(dim_state, dim_ens, nfields, dim_fields, offsets, &
       remove_mean, do_mv, states, stddev, svals, svdU, meanstate, status_covar)


! *********************************************************
! *** Write mean state and decomposed covariance matrix ***
! *********************************************************

! *** Determine rank to write ***

  getlimit: DO i = 1, dim_ens
     IF (svals(i) >= limit) THEN
        rank = i
     ELSE
        EXIT getlimit
     END IF
  END DO getlimit
  IF (rank < dim_ens) THEN
     WRITE (*,'(1x,a,i6,a,es10.2)') &
          'Use maximum of ', rank, ' eigenvectors due to eigenvalue-limit of ',limit
  END IF
  IF (rank == dim_ens) THEN
     rank = dim_ens - 1
     WRITE (*,'(5x,a,i4)') '++ reset rank to ',rank
  END IF

!  rank = desired_rank
  WRITE (*,'(5x,a,i4)') '++ hard setting rank to ',rank

  WRITE (*,'(5x,a)') 'Singular values: '
  DO i = 1, rank
    WRITE (*, '(10x, i4, es12.3)') i, svals(i)
  END DO


  WRITE (*,'(/1x,a)') '------- Write decomposed covariance matrix and mean state -------------'

  ! Write singular values
  open(22, file='singular_values',status='replace')
  write(22,*) svals(:)
  close(22)


  ! Write mean state
  open(22, file='mean_state',status='replace')
  write(22,*) meanstate(:)
  close(22)

  ! *** Write singular vectors

  open(22, file='singular_vector',status='replace')
  writevectors: DO i = 1, rank
    ! Write singular vector
    write(22,*) svdU(:,i)
  END DO writevectors
    close(22)

! ********************
! *** Finishing up ***
! ********************

  DEALLOCATE(states, meanstate)
  DEALLOCATE(svals, svdU)

  WRITE (*,'(/1x,a/)') '------- END -------------'

END PROGRAM generate_covar

